plugins {
    id "application"
    id "de.jjohannes.extra-java-module-info" version "0.5"
}

group "org.examples"
version "1.0-SNAPSHOT"

def djlVersion = "0.12.0"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configure(this) {
    apply from: file("${rootProject.projectDir}/../tools/gradle/formatter.gradle")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    modularity.inferModulePath = true
}

compileJava {
    inputs.property("moduleName", "org.examples.module")
    doFirst {
        options.compilerArgs.addAll([
                "--module-path", classpath.asPath,
                "--add-modules", "org.examples.module"
        ])
        classpath = files()
    }
}

extraJavaModuleInfo {
    automaticModule("api-${djlVersion}.jar", "ai.djl.api")
    automaticModule("tensorflow-model-zoo-${djlVersion}.jar", "ai.djl.tensorflow_model_zoo")
    automaticModule("tensorflow-engine-${djlVersion}.jar", "ai.djl.tensorflow_engine")
    automaticModule("tensorflow-api-${djlVersion}.jar", "ai.djl.tensorflow_api")
    automaticModule("tensorflow-native-auto-2.3.1.jar", "ai.djl.tensorflow_native")
    automaticModule("protobuf-java-3.8.0.jar", "com.google.protobuf.protobuf_java")
}

dependencies {
    implementation platform("ai.djl:bom:${djlVersion}")
    implementation "ai.djl:api"
    implementation "ai.djl.tensorflow:tensorflow-model-zoo"
    implementation "ai.djl.tensorflow:tensorflow-native-auto"

    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.17.0"
}

application {
    mainModule = "org.examples.module"
    getMainClass().set(System.getProperty("main", "org.examples.module.ModuleTest"))
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    systemProperty("disableProgressBar", "true")
}
tasks.distTar.enabled = false
