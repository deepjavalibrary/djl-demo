plugins {
    id 'scala'
    id 'java'
}

group "com.example"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    exclusion
}

dependencies {
    implementation platform("ai.djl:bom:${djl_version}")
    implementation "org.apache.spark:spark-sql_2.12:3.0.1"
    implementation "org.apache.spark:spark-mllib_2.12:3.0.1"
    implementation "org.apache.hadoop:hadoop-hdfs:2.7.4"
    // exclude system provided dependencies to avoid version mismatch
    exclusion "org.apache.spark:spark-sql_2.12:3.0.1"
    exclusion "org.apache.spark:spark-mllib_2.12:3.0.1"
    exclusion "org.apache.hadoop:hadoop-hdfs:2.7.4"
    implementation "ai.djl:api"

    runtimeOnly "ai.djl.pytorch:pytorch-model-zoo"
    runtimeOnly "ai.djl.pytorch:pytorch-jni"
    // TODO: remove version after 0.22.0 bom fix
    runtimeOnly "ai.djl.pytorch:pytorch-native-cu117:1.13.1:linux-x86_64"
    runtimeOnly "org.slf4j:slf4j-simple:${slf4j_simple_version}"
}

configure(this) {
    defaultTasks "jar"
}

compileScala {
    scalaCompileOptions.setAdditionalParameters(["-target:jvm-1.8"])
}

jar {
    from {
        (configurations.runtimeClasspath - configurations.exclusion).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.WARN
    manifest {
        attributes["Main-Class"] = "com.examples.ImageClassificationExample"
    }
}
