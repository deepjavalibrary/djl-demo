plugins {
    id "application"
    id "de.jjohannes.extra-java-module-info" version "0.11"
    id 'org.beryx.jlink' version '2.26.0'
}

group "org.examples"
version "1.0"

def djlVersion = "0.20.0"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

jlink {
    forceMerge "log4j-api", "gson"
    launcher {
        name = "djl-module-demo"
    }
}

configure(this) {
    apply from: file("${rootProject.projectDir}/../../tools/gradle/formatter.gradle")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    // modularity.inferModulePath = true
}

extraJavaModuleInfo {
    automaticModule("opencv-4.5.1-2.jar", "org.openpnp.opencv")
}

dependencies {
    implementation platform("ai.djl:bom:${djlVersion}")
    implementation "ai.djl:api"
    implementation "ai.djl.pytorch:pytorch-model-zoo"
    implementation "ai.djl.opencv:opencv"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.17.2"
}

application {
    mainModule = "org.examples.module"
    mainClass = "org.examples.module.ModuleTest"
}

run {
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
}
tasks.distTar.enabled = false
