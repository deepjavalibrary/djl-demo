apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "ai.djl.examples.quickdraw"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

task downloadModel {
    doLast {
        def url = "https://alpha-djl-demos.s3.amazonaws.com/model/quickdraw"
        def files = ["doodle_mobilenet.pt", "synset.txt"]
        files.each{ entry ->
            project.logger.lifecycle("Downloading ${url}/${entry}")
            def file = new File("${project.projectDir}/src/main/assets/models/${entry}")
            file.getParentFile().mkdirs()
            new URL("${url}/${entry}").withInputStream { i -> file.withOutputStream { it << i } }
        }
    }
}

assemble.dependsOn downloadModel

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'ai.djl:api:0.6.0-SNAPSHOT'
    // TODO: Bump up android version after fix
    implementation 'ai.djl.android:core:0.5.0-SNAPSHOT'
    runtimeOnly 'ai.djl.pytorch:pytorch-engine:0.6.0-SNAPSHOT'
    runtimeOnly 'ai.djl.android:pytorch-native:0.5.0-SNAPSHOT'
}
