plugins {
    id "java"
    id "application"
}

group "com.example"
version "1.0-SNAPSHOT"

def djlVersion = getEnv("DJL_VERSION", "0.28.0-SNAPSHOT")
def engine = getEnv("DJL_ENGINE", "pytorch-native-auto")
def os = getOsName()
def arch = System.getProperty("os.arch") == "amd64" ? "x86_64" : System.getProperty("os.arch")
def stagingRepo = getEnv("DJL_STAGING", null)
def ptVersion = getEnv("PT_VERSION", "2.1.2")
def ptJniVersion = ptVersion.replaceAll("-SNAPSHOT", "")

repositories {
    mavenCentral()
    mavenLocal()
    if (stagingRepo != null) {
        for (String repo : stagingRepo.split(",")) {
            maven {
                url "https://oss.sonatype.org/service/local/repositories/${repo}/content/"
            }
        }
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configure(this) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    apply from: file("${rootProject.projectDir}/../tools/gradle/formatter.gradle")
}

dependencies {
    implementation platform("ai.djl:bom:${djlVersion}")
    implementation "ai.djl.sentencepiece:sentencepiece"
    implementation "ai.djl.huggingface:tokenizers"

    implementation "commons-cli:commons-cli:${commons_cli_version}"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"

    if (engine.startsWith("pytorch")) {
        runtimeOnly "ai.djl.pytorch:pytorch-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.pytorch:pytorch-jni:${ptJniVersion}-${djlVersion}!!"
            runtimeOnly "ai.djl.pytorch:${engine}:${ptVersion}!!:${os}-${arch}"
        }
        runtimeOnly "ai.djl:basicdataset"
        runtimeOnly "ai.djl:model-zoo"
        runtimeOnly "ai.djl.aws:aws-ai"
        runtimeOnly "ai.djl.hadoop:hadoop"
    } else if (engine.startsWith("tensorflow")) {
        runtimeOnly "ai.djl.tensorflow:tensorflow-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.tensorflow:${engine}::${os}-x86_64"
        }
    } else if (engine.startsWith("onnxruntime")) {
        runtimeOnly "ai.djl.onnxruntime:onnxruntime-engine"
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("paddlepaddle")) {
        runtimeOnly "ai.djl.paddlepaddle:paddlepaddle-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.paddlepaddle:${engine}::${os}-x86_64"
        }
        implementation "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("fasttext")) {
        runtimeOnly "ai.djl.fasttext:fasttext-engine"
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("tflite")) {
        runtimeOnly "ai.djl.tflite:tflite-engine"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.tflite:${engine}::${os}-x86_64"
        }
    } else if (engine.startsWith("xgboost")) {
        if (engine == "xgboost-gpu") {
            runtimeOnly "ai.djl.ml.xgboost:xgboost-gpu"
            runtimeOnly "ai.rapids:cudf:${rapis_version}:cuda11"
        } else {
            runtimeOnly "ai.djl.ml.xgboost:xgboost"
        }
    } else if (engine.startsWith("lightgbm")) {
        runtimeOnly "ai.djl.ml.lightgbm:lightgbm"
    } else if (engine.startsWith("tensorrt")) {
        runtimeOnly "ai.djl.tensorrt:tensorrt"
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("python")) {
        runtimeOnly "ai.djl.python:python"
    } else if (engine.startsWith("tokenizers")) {
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("sentencepiece")) {
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("mxnet")) {
        runtimeOnly "ai.djl.mxnet:mxnet-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.mxnet:${engine}::${os}-x86_64"
        }
    } else {
        throw new GradleException("Unsupported engine: ${engine}.")
    }
}

application {
    mainClass = System.getProperty("main", "ai.djl.canary.CanaryTest")
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    systemProperty("disableProgressBar", "true")
}
tasks.distTar.enabled = false

static def getOsName() {
    String osName = System.getProperty("os.name")
    if (osName.startsWith("Win")) {
        return "win"
    } else if (osName.startsWith("Mac")) {
        return "osx"
    } else if (osName.startsWith("Linux")) {
        return "linux"
    } else {
        throw new GradleException("Unsupported os: " + osName)
    }
}

static def getEnv(String key, String defaultValue) {
    String value = System.getenv(key)
    return value == null || value.isEmpty() ? defaultValue : value
}
