plugins {
    id "java"
    id "application"
}

group "com.example"
version "1.0-SNAPSHOT"

def djlVersion = getEnv("DJL_VERSION", "0.18.0")
def engine = getEnv("DJL_ENGINE", "mxnet-native-auto")
def os = getOsName()
def stagingRepo = getEnv("DJL_STAGING", null)
def ptVersion = getEnv("PT_VERSION", "1.11.0")

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/service/local/repositories/${stagingRepo}/content/"
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configure(this) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    apply from: file("${rootProject.projectDir}/../tools/gradle/formatter.gradle")
}

dependencies {
    implementation platform("ai.djl:bom:${djlVersion}")
    implementation "ai.djl.sentencepiece:sentencepiece"

    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"

    if (engine.startsWith("pytorch")) {
        runtimeOnly "ai.djl.pytorch:pytorch-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.pytorch:pytorch-jni:${ptVersion}-${djlVersion}!!"
            runtimeOnly "ai.djl.pytorch:${engine}:${ptVersion}!!:${os}-x86_64"
        }
    } else if (engine.startsWith("tensorflow")) {
        runtimeOnly "ai.djl.tensorflow:tensorflow-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.tensorflow:${engine}::${os}-x86_64"
        }
    } else if (engine.startsWith("onnxruntime")) {
        runtimeOnly "ai.djl.onnxruntime:onnxruntime-engine"
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("paddlepaddle")) {
        runtimeOnly "ai.djl.paddlepaddle:paddlepaddle-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.paddlepaddle:${engine}::${os}-x86_64"
        }
        implementation "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("dlr")) {
        runtimeOnly "ai.djl.dlr:dlr-engine"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.dlr:${engine}::${os}-x86_64"
        }
        runtimeOnly "ai.djl.pytorch:pytorch-engine"

        runtimeOnly "ai.djl.fasttext:fasttext-engine"
    } else if (engine.startsWith("tflite")) {
        runtimeOnly "ai.djl.tflite:tflite-engine"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.tflite:${engine}::${os}-x86_64"
        }
    } else if (engine.startsWith("xgboost")) {
        runtimeOnly "ai.djl.ml.xgboost:xgboost"
    } else if (engine.startsWith("tensorrt")) {
        runtimeOnly "ai.djl.tensorrt:tensorrt"
        runtimeOnly "ai.djl.pytorch:pytorch-engine"
    } else if (engine.startsWith("python")) {
        runtimeOnly "ai.djl.python:python"
    } else {
        runtimeOnly "ai.djl.mxnet:mxnet-model-zoo"
        if (!engine.contains("-auto")) {
            runtimeOnly "ai.djl.mxnet:${engine}::${os}-x86_64"
        }
        runtimeOnly "ai.djl:basicdataset"
        runtimeOnly "ai.djl:model-zoo"
        runtimeOnly "ai.djl.aws:aws-ai"
        runtimeOnly "ai.djl.hadoop:hadoop"
    }
}

application {
    mainClass = System.getProperty("main", "ai.djl.canary.CanaryTest")
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    systemProperty("disableProgressBar", "true")
}
tasks.distTar.enabled = false

static def getOsName() {
    String osName = System.getProperty("os.name")
    if (osName.startsWith("Win")) {
        return "win"
    } else if (osName.startsWith("Mac")) {
        return "osx"
    } else if (osName.startsWith("Linux")) {
        return "linux"
    } else {
        throw new GradleException("Unsupported os: " + osName)
    }
}

static def getEnv(String key, String defaultValue) {
    String value = System.getenv(key)
    return value == null || value.isEmpty() ? defaultValue : value
}
